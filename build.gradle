import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript{
    repositories{
        mavenLocal()
        maven{url "https://repo1.maven.org/maven2"}
    }
}

plugins{
    id 'org.openapi.generator' version '6.2.1'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'net.nemerosa.versioning' version '2.8.2'
}

ext {
    junitVersion = '5.9.1'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:9.1.0'

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.10.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'io.gsonfire', name: 'gson-fire', version: '1.8.5'
    implementation group: 'io.swagger.codegen.v3', name: 'swagger-generator', version: '3.0.36'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations-jakarta', version: '2.2.8'
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.2.8'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

openApiGenerate{
    generatorName = 'java'
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    configFile = "$rootDir/src/main/resources/config.yaml"
    apiPackage = "org.lankheet.domiot.client"
    outputDir = "$buildDir/generated/java"
    configOptions = [
            useJakartaEe : "true"
    ]
}

openApiValidate{
    inputSpec = "${rootDir}/src/main/resources/openapi.yaml"
}

tasks.register('openaApiGenerateTs', GenerateTask) {
    generatorName = "typescript-angular"
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated/ts"
    apiPackage = "org.lankheet.domiot.api"

    configOptions = [
            ngVersion : "15.2.0",
            npmName   : "${project.name}-client",
            npmVersion: "${project.version}",
            modelTests: "false",
    ]
}

tasks.register('copyGenerated', Copy) {
    from "${rootDir}/build/generated/java/src/main/java"
    into "${rootDir}/src/main/java"
}

tasks.register('packageNpmLib') {
    doLast {
        exec {
            workingDir "${buildDir}/generated/ts"
            executable 'npx'
            args 'ng', 'build', "${project.name}-client"
        }
        println "Executed!"
    }
}

tasks.packageNpmLib.dependsOn('openaApiGenerateTs')
tasks.copyGenerated.dependsOn('openApiGenerate')
tasks.openApiGenerate.dependsOn('openApiValidate')
compileJava.dependsOn('copyGenerated')


publishing{
    publications{
        pluginMaven(MavenPublication){
            groupId findProperty('group')
            artifactId findProperty('name')
            version findProperty('version')
        }
    }
}

clean {
    delete 'src/main/java'
}
